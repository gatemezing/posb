

Queries Category 1
==================

Q1:
===
 ## Goal: Compute non-inferred symmetric concise bounded description of an entire FRBR hierarchy.  
 #DEFINE sql:describe-mode "SCBD"

prefix cdm: <http://publications.europa.eu/ontology/cdm#>
describe ?w ?e ?m where
{	# substitute cellar id here 
	values ?w {<http://publications.europa.eu/resource/cellar/6df75a83-c084-11e3-86f9-01aa75ed71a1>}   
	?e cdm:expression_belongs_to_work ?w.
	optional{?m cdm:manifestation_manifests_expression ?e.}
}



Q1.cypher
=========

MATCH (e:Resource)-[r:expression_belongs_to_work]->(w:Resource {uri:'http://publications.europa.eu/resource/cellar/6df75a83-c084-11e3-86f9-01aa75ed71a1'})
OPTIONAL MATCH
(m:Resource)-[r1:manifestation_manifests_expression]->(e:Resource)
RETURN r, e, w


 
 
Q2
 
prefix cdm: <http://publications.europa.eu/ontology/cdm#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
 
describe ?w ?e ?m where
{
	
	values ?w {<http://publications.europa.eu/resource/cellar/6df75a83-c084-11e3-86f9-01aa75ed71a1>}
	?e cdm:expression_belongs_to_work ?w;
	# substitute language code here
	cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/POR>.
	optional{?m cdm:manifestation_manifests_expression ?e.}
}
 
Q2.cypher 

MATCH (l:Resource {uri:'http://publications.europa.eu/resource/authority/language/POR'})<-[r1:expression_uses_language]-(e:Resource)-[r2:expression_belongs_to_work]->(w:Resource {uri:'http://publications.europa.eu/resource/cellar/6df75a83-c084-11e3-86f9-01aa75ed71a1'})
OPTIONAL MATCH
(m:Resource)-[r3:manifestation_manifests_expression]->(e:Resource)
RETURN w, m, l
 
 
 
 
 
Q3:
 
## Goal: Compute non-inferred concise bounded branch description
##Note that this time we compute the concise bounded description of a branch notice, not its
## symmetric equivalent. CDB is enough for representation of embedded works. 
## Requirements:We intend to use the query for on-the-fly notice generation. 
 
#DEFINE sql:describe-mode "CBD"
prefix cdm: <http://publications.europa.eu/ontology/cdm#> 
prefix owl: <http://www.w3.org/2002/07/owl#> 
 
describe ?s ?e ?m where {
	# substitute cellar id here
	values ?w {<http://publications.europa.eu/resource/cellar/6df75a83-c084-11e3-86f9-01aa75ed71a1>}
	?s owl:sameAs? ?w.
	?e cdm:expression_belongs_to_work ?s;
	# substitute language code here
	cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/POR>.
	optional{?m cdm:manifestation_manifests_expression ?e.}
}
 
Q3.cypher
 
MATCH (s:Resource)-[r1:sameAs*]->(w:Resource {uri:'http://publications.europa.eu/resource/cellar/6df75a83-c084-11e3-86f9-01aa75ed71a1'}) 
MATCH (l:Resource {uri:'http://publications.europa.eu/resource/authority/language/POR'})<-[r1:expression_uses_language]-(e:Resource)-[r2:expression_belongs_to_work]->(w:Resource {uri:'http://publications.europa.eu/resource/cellar/6df75a83-c084-11e3-86f9-01aa75ed71a1'})
OPTIONAL MATCH
(m:Resource)-[r3:manifestation_manifests_expression]->(e:Resource)
RETURN s, w, m, l
 
 
 
Q4:
 
#Q4. %%%%%%%%%%%%%%%%%%%%%%%%%%%%
## Goal: Retrieve language specific information required for the decoding of concepts from named authority lists. 
## Remarks: Input = concept uri + 2-char language code. In order to harmonize and standardize the codes and the associated labels used in the Publications Office
## the MDR team maintains a number of tables, controlled vocabularies or value lists.
## Requirements: We intend to use the query for on-the-fly notice generation.
 
prefix cdm: <http://publications.europa.eu/ontology/cdm#> 
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix at: <http://publications.europa.eu/ontology/authority> 
 
construct
{
	?s ?p ?o.
}
where
{
	# substitute concept uri here
	values ?s {<http://publications.europa.eu/resource/authority/language/POR>}
	{
	select ?s ?o ?p where
	{
		?s ?p ?o.
		bind(lang(?o) as ?lang)
		filter( ?lang = "" )
		}
	} 
	UNION
	{
		select ?s ?o ?p where 
		{
		?s ?p ?o.
		bind(lang(?o) as ?lang)
		# substitute language code here
		filter( ?lang = "en" )
		}
	}
} 
 

Q5:
 
#############################################################################
#Q5: Search by subject-author faced applied - 2 selections-year facetApplied #
#############################################################################
 
PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX eurovoc: <http://eurovoc.europa.eu/schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
 
 
 
SELECT ?work 
      ?expr
      (?lang1 as ?lang) 
      (group_concat(distinct ?title_;separator=", ") as ?title)  
      ?date
      (?abstract as ?abstracts )
      (group_concat(distinct ?workId;separator=", ") as ?workIds) 
      (group_concat(distinct ?workType;separator=", ") as ?workTypes) 
      (group_concat(distinct ?subject;separator=", ") as ?subjects) 
      (group_concat(distinct ?author;separator=", ") as ?authors)
      (group_concat(distinct ?work;separator=", ") as ?cellarURIs)
      (group_concat(distinct ?mtype;separator=", ") as ?mtypes) 
      (group_concat(distinct ?thumbnail;separator=", ") as ?thumbnails)
WHERE {
      ?work cdm:work_id_document ?workId;
         rdf:type ?workType.
      optional{?work cdm:work_is_about_concept_eurovoc/dc:identifier ?subject.}
      optional{?work cdm:work_created_by_agent/dc:identifier ?author.}
      ?expr cdm:expression_belongs_to_work ?work.
      optional {?expr cdm:expression_abstract ?abstract}.
      optional {?m cdm:manifestation_manifests_expression ?expr; cdm:manifestation_type ?mtype.}
      optional {?m2  cdm:manifestation_manifests_expression ?expr; cdm:manifestation_has_thumbnail ?thumbnail }.
      ?expr cdm:expression_uses_language/dc:identifier ?lang1.
      optional { ?expr cdm:expression_title ?title_ }.
      {
          SELECT ?work ?date
          WHERE
          {
            {
              SELECT DISTINCT ?work ?date
              WHERE
                {
                
                ?work cdm:work_is_about_concept_eurovoc/skos:inScheme/dc:identifier "4026";
 
                    cdm:work_date_document ?date.                   
                 filter(YEAR(?date) in (2014)).
?work cdm:work_created_by_agent/dc:identifier ?authorCode2 .
FILTER ( ?authorCode2 in ("CURIA","GCEU")).
 
                 } 
              ORDER BY DESC(?date)
            }
          }
          OFFSET 0
          LIMIT 10 }
      }
GROUP BY ?work ?expr ?date ?abstract ?lang1
ORDER BY DESC(?date) ?work
 
 
Q6
###############################
##Q6: Dynamic Query ###########
###############################
 
PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX eurovoc: <http://eurovoc.europa.eu/schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
SELECT ?work 
       ((?abstract) as ?abstracts) 
       (group_concat(distinct ?title_;separator=", ") as ?title)  
       ?date
      (group_concat(distinct ?workId;separator=", ") as ?workIds) 
      (group_concat(distinct ?workType;separator=", ") as ?workTypes) 
      (group_concat(distinct ?subject;separator=", ") as ?subjects) 
      (group_concat(distinct ?author;separator=", ") as ?authors)
      (group_concat(distinct ?work;separator=", ") as ?cellarURIs)
      (group_concat(distinct ?mtype;separator=", ") as ?mtypes)WHERE {
   ?work cdm:work_is_about_concept_eurovoc/dc:identifier ?subject;
   cdm:work_id_document ?workId;
   rdf:type ?workType;
   cdm:work_created_by_agent/dc:identifier ?author.
   ?expr cdm:expression_belongs_to_work ?work.
   optional {?expr cdm:expression_title ?title_;
                   cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG>.}
   optional {?expr cdm:expression_abstract ?abstract}.
   optional {?m cdm:manifestation_manifests_expression ?expr; cdm:manifestation_type ?mtype}
{
   SELECT DISTINCT ?work ?date WHERE {
?work cdm:work_is_about_concept_eurovoc/dc:identifier "3493";
      cdm:work_date_document ?date.
?expr2 cdm:expression_belongs_to_work ?work.
 ?expr2 cdm:expression_title ?title2;
      cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG>. 
?work cdm:work_created_by_agent/dc:identifier "EC".
}
ORDER BY DESC(?date)
OFFSET 0
LIMIT 10 }
}
GROUP BY ?work ?date ?abstract


Q6.cypher
:params { concept_id: "3493", agent_id: "EP", lang_uri: "http://publications.europa.eu/resource/authority/language/ENG" }

// Parameterising the query is best practice but also makes it easy to run tests with different values instead of hardcoding. 
// I've tried other combinations:
//       concept_id: "3493", agent_id: "OP_DATPRO" 
//       concept_id: "5404", agent_id: "EC" 

match ({ uri: $lang_uri })<-[:expression_uses_language]-(expr)-[:expression_belongs_to_work]->(work)-[:work_is_about_concept_eurovoc]->(:Concept {identifier: $concept_id}),  (work)-[:work_created_by_agent]->({identifier: $agent_id })
with work, work.work_date_document as date order by date desc limit 10
match (a)-[:work_created_by_agent]-(work:Resource)-[:work_is_about_concept_eurovoc]->(c) 
match (exl)<-[:expression_uses_language]-(expr)-[:expression_belongs_to_work]->(work)
optional match (man)-[:manifestation_manifests_expression]->(expr)
return  work.uri as work, 
        expr.expression_abstract as abstract, 
        collect(distinct case when exl.uri = $lang_uri then expr.expression_title else "" end ) as title, 
	work.work_date_document as date, 
	collect(distinct work.work_id_document) as workIds, 
	collect(distinct labels(work)) as workTypes, 
	collect( distinct c.identifier) as subjects, 
	collect( distinct a.identifier) as authors, 
	collect( distinct man.manifestation_type) as mtypes





##################################################
##Q7: Search by subject - author faced applied ###
##################################################

PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX eurovoc: <http://eurovoc.europa.eu/schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
SELECT ?work 
      ?expr
      (?lang1 as ?lang) 
      (group_concat(distinct ?title_;separator=", ") as ?title)  
      ?date
      (?abstract as ?abstracts )
      (group_concat(distinct ?workId;separator=", ") as ?workIds) 
      (group_concat(distinct ?workType;separator=", ") as ?workTypes) 
      (group_concat(distinct ?subject;separator=", ") as ?subjects) 
      (group_concat(distinct ?author;separator=", ") as ?authors)
      (group_concat(distinct ?work;separator=", ") as ?cellarURIs)
      (group_concat(distinct ?mtype;separator=", ") as ?mtypes) 
      (group_concat(distinct ?thumbnail;separator=", ") as ?thumbnails)
WHERE {
      ?work cdm:work_id_document ?workId;
         rdf:type ?workType.
      optional{?work cdm:work_is_about_concept_eurovoc/dc:identifier ?subject.}
      optional{?work cdm:work_created_by_agent/dc:identifier ?author.}
      ?expr cdm:expression_belongs_to_work ?work.
      optional {?expr cdm:expression_abstract ?abstract}.
      optional {?m cdm:manifestation_manifests_expression ?expr; cdm:manifestation_type ?mtype.}
      optional {?m2  cdm:manifestation_manifests_expression ?expr; cdm:manifestation_has_thumbnail ?thumbnail }.
      ?expr cdm:expression_uses_language/dc:identifier ?lang1.
      optional { ?expr cdm:expression_title ?title_ }.
      {
          SELECT ?work ?date
          WHERE
          {
            {
              SELECT DISTINCT ?work ?date
              WHERE
                {
                
                ?work cdm:work_is_about_concept_eurovoc/skos:inScheme/dc:identifier "4026";

                    cdm:work_date_document ?date.                   
                 ?work cdm:work_created_by_agent/dc:identifier "CURIA".

                 } 
              ORDER BY DESC(?date)
            }
          }
          OFFSET 0
          LIMIT 10 }
      }
GROUP BY ?work ?expr ?date ?abstract ?lang1
ORDER BY DESC(?date) ?work

#################################################################
## Q8: Search by subject - author faceted applied-2 selections ##
#################################################################

PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX eurovoc: <http://eurovoc.europa.eu/schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
SELECT ?work 
      ?expr
      (?lang1 as ?lang) 
      (group_concat(distinct ?title_;separator=", ") as ?title)  
      ?date
      (?abstract as ?abstracts )
      (group_concat(distinct ?workId;separator=", ") as ?workIds) 
      (group_concat(distinct ?workType;separator=", ") as ?workTypes) 
      (group_concat(distinct ?subject;separator=", ") as ?subjects) 
      (group_concat(distinct ?author;separator=", ") as ?authors)
      (group_concat(distinct ?work;separator=", ") as ?cellarURIs)
      (group_concat(distinct ?mtype;separator=", ") as ?mtypes) 
      (group_concat(distinct ?thumbnail;separator=", ") as ?thumbnails)
WHERE {
      ?work cdm:work_id_document ?workId;
         rdf:type ?workType.
      optional{?work cdm:work_is_about_concept_eurovoc/dc:identifier ?subject.}
      optional{?work cdm:work_created_by_agent/dc:identifier ?author.}
      ?expr cdm:expression_belongs_to_work ?work.
      optional {?expr cdm:expression_abstract ?abstract}.
      optional {?m cdm:manifestation_manifests_expression ?expr; cdm:manifestation_type ?mtype.}
      optional {?m2  cdm:manifestation_manifests_expression ?expr; cdm:manifestation_has_thumbnail ?thumbnail }.
      ?expr cdm:expression_uses_language/dc:identifier ?lang1.
      optional { ?expr cdm:expression_title ?title_ }.
      {
          SELECT ?work ?date
          WHERE
          {
            {
              SELECT DISTINCT ?work ?date
              WHERE
                {
                
                ?work cdm:work_is_about_concept_eurovoc/skos:inScheme/dc:identifier "4026";

                    cdm:work_date_document ?date.                   
                 ?work cdm:work_created_by_agent/dc:identifier ?authorCode2 .
FILTER ( ?authorCode2 in ("CURIA","GCEU")).

                 } 
              ORDER BY DESC(?date)
            }
          }
          OFFSET 0
          LIMIT 10 }
      }
GROUP BY ?work ?expr ?date ?abstract ?lang1
ORDER BY DESC(?date) ?work


###################################
##Q9: Search by Subject only ######
###################################

PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX eurovoc: <http://eurovoc.europa.eu/schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
SELECT ?work 
      ?expr
      (?lang1 as ?lang) 
      (group_concat(distinct ?title_;separator=", ") as ?title)  
      ?date
      (?abstract as ?abstracts )
      (group_concat(distinct ?workId;separator=", ") as ?workIds) 
      (group_concat(distinct ?workType;separator=", ") as ?workTypes) 
      (group_concat(distinct ?subject;separator=", ") as ?subjects) 
      (group_concat(distinct ?author;separator=", ") as ?authors)
      (group_concat(distinct ?work;separator=", ") as ?cellarURIs)
      (group_concat(distinct ?mtype;separator=", ") as ?mtypes) 
      (group_concat(distinct ?thumbnail;separator=", ") as ?thumbnails)
WHERE {
      ?work cdm:work_id_document ?workId;
         rdf:type ?workType.
      optional{?work cdm:work_is_about_concept_eurovoc/dc:identifier ?subject.}
      optional{?work cdm:work_created_by_agent/dc:identifier ?author.}
      ?expr cdm:expression_belongs_to_work ?work.
      optional {?expr cdm:expression_abstract ?abstract}.
      optional {?m cdm:manifestation_manifests_expression ?expr; cdm:manifestation_type ?mtype.}
      optional {?m2  cdm:manifestation_manifests_expression ?expr; cdm:manifestation_has_thumbnail ?thumbnail }.
      ?expr cdm:expression_uses_language/dc:identifier ?lang1.
      optional { ?expr cdm:expression_title ?title_ }.
      {
          SELECT ?work ?date
          WHERE
          {
            {
              SELECT DISTINCT ?work ?date
              WHERE
                {
                
                ?work cdm:work_is_about_concept_eurovoc/skos:inScheme/dc:identifier "4026";

                    cdm:work_date_document ?date.                   
                 
                 } 
              ORDER BY DESC(?date)
            }
          }
          OFFSET 0
          LIMIT 10 }
      }
GROUP BY ?work ?expr ?date ?abstract ?lang1
ORDER BY DESC(?date) ?work


Q9.cypher

:params { scheme_id: "4026" }


match (w:work)-[:work_is_about_concept_eurovoc]->(:Concept)-[:inScheme]->( { identifier: $scheme_id})
with w, w.work_date_document as date order by date desc skip 0 limit 10
match (l)<-[:expression_uses_language]-(e)-[:expression_belongs_to_work]->(w)-[:work_is_about_concept_eurovoc]->(c)
optional match (w)-[:work_created_by_agent]->(a)
optional match (m)-[:manifestation_manifests_expression]->(e)
return w.uri as work, 
       e.uri as expr, 
       l.identifier as lang1, 
       e.expression_title as title, 
       w.work_date_document as date, 
       e.expression_abstract as abstract, 
       collect(w.work_id_document) as workIds, 
       collect(distinct labels(w)) as workTypes, 
       collect(distinct c.identifier) as subjects, 
       collect(distinct a.identifier) as authors, 
       collect(distinct m.manifestation_type) as mtypes, 
       collect(distinct m.manifestation_has_thumbnail) as thumbnails



############################################################################
##Q10: Unsatisfactory use case on the wizard without answer - no response ##
### It simulates select all filed and filter on title like "microsoft" #####
############################################################################

PREFIX cdm:<http://publications.europa.eu/ontology/cdm#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>

SELECT DISTINCT  (group_concat(distinct ?work;separator=",") as ?cellarURIs) 
   (group_concat(distinct ?title_;separator=",") as ?title) 
  ?langIdentifier
   (group_concat(distinct ?mtype;separator=",") as ?mtypes) 
   (group_concat(distinct ?thumbnail;separator=",") as ?thumbnails) 
  (group_concat(distinct ?resType;separator=",") as ?workTypes) 
  (group_concat(distinct ?agentName;separator=",") as ?authors) 
  ?date
  (group_concat(distinct ?subjectLabel;separator=",") as ?subjects) 
  (group_concat(distinct ?workId_;separator=",") as ?workIds) 

WHERE 
{
?work cdm:work_date_document ?d.
?exp cdm:expression_belongs_to_work ?work .
?exp cdm:expression_title ?title_ .
?exp cdm:expression_uses_language/dc:identifier ?langIdentifier .
?manif cdm:manifestation_manifests_expression ?exp .
?manif cdm:manifestation_type ?mtype .
OPTIONAL {?manif2 cdm:manifestation_manifests_expression ?exp }.
OPTIONAL {?manif2 cdm:manifestation_has_thumbnail ?thumbnail }.
?work rdf:type ?resType .
OPTIONAL {?work cdm:work_created_by_agent/skos:prefLabel ?agentName .
filter (lang(?agentName)="en")}.
?work cdm:work_date_document ?date .
OPTIONAL {?work cdm:work_is_about_concept_eurovoc/skos:prefLabel ?subjectLabel .
filter (lang(?subjectLabel)="en")}.
?work cdm:work_id_document ?workId_ .
 ?exp cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG>.
FILTER (regex( str(?title_), "microsoft", "i")).
}
GROUP BY ?work ?date ?langIdentifier
LIMIT 100
OFFSET 0


## Q11: Count number of people per named graph ###
## each directory is stored as a named graph ###


prefix cdm: <http://publications.europa.eu/ontology/cdm#> 
PREFIX org: <http://www.w3.org/ns/org#>
prefix vcard: <http://www.w3.org/2006/vcard/ns#>
prefix foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

select distinct ?g (count(?Person) as ?Number)  where {
graph ?g {
?Person rdf:type foaf:Person.
}
} group by ?g order by desc(?Number)


## Q12: Normalized query used in PROD ###################################
## NIM tab Default 
## sample: <http://publications.europa.eu/resource/celex/31971L0317> 
## BGP: 17
###########################################################################
prefix cdm: <http://publications.europa.eu/ontology/cdm#> 
prefix annotation: <http://publications.europa.eu/ontology/annotation#> 
prefix owl: <http://www.w3.org/2002/07/owl#>


select distinct ?country ?nim_id ?work_date_document ?transposition_deadline ?transposition_status ?oj_name ?oj_number ?oj_date ?oj_page ?expr_language ?title ?entry_into_force ?reference_commission ?date_notification ?website ?eli (group_concat(distinct ?type;separator=", ") as ?types) 
where 
{ ?cellar_id owl:sameAs ?nim_id. filter (regex(?nim_id,'resource/nim')).
{ select distinct ?country ?cellar_id ?work_date_document ?transposition_deadline ?transposition_status ?oj_name ?oj_number ?oj_date ?oj_page ?expr_language ?title ?entry_into_force 
	?reference_commission ?date_notification ?website ?eli ?type 
  where { ?cellar_id cdm:measure_national_implementing_implements_directive/owl:sameAs <http://publications.europa.eu/resource/celex/31971L0317>.
  	?exp cdm:expression_belongs_to_work ?cellar_id ; cdm:expression_uses_language ?expr_language; cdm:expression_title ?title. 
  OPTIONAL {?manif cdm:manifestation_manifests_expression ?exp; cdm:manifestation_type ?type.} 
  OPTIONAL { ?cellar_id cdm:measure_national_implementing_implemented_by_country ?country.} 
  OPTIONAL { ?s annotation:transposition_deadline_transmitted ?transposition_deadline_transmitted. 
    OPTIONAL {?s annotation:transposition_notification ?date_notification.} ?s owl:annotatedSource ?cellar_id. 
    ?s owl:annotatedProperty cdm:measure_national_implementing_implements_directive. 
    ?s owl:annotatedTarget/owl:sameAs <http://publications.europa.eu/resource/celex/31971L0317>.} 
    bind((if(bound(?transposition_deadline_transmitted), ?transposition_deadline_transmitted,"1001-01-01")) as ?transposition_deadline). 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_declaration_transposition_member-state ?transposition_status.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_name_official_journal ?oj_name.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_number_official_journal ?oj_number.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_date_official_journal ?oj_date.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_page_official_journal ?oj_page.} 
    OPTIONAL {?cellar_id cdm:resource_legal_date_entry-into-force ?entry_into_force.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_reference_commission ?reference_commission.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_national_website_link ?website.} 
    OPTIONAL {?cellar_id cdm:eli ?eli.} 
    OPTIONAL {?cellar_id cdm:work_date_document ?work_date_document.}}}} 
    GROUP BY ?country ?nim_id ?work_date_document ?transposition_deadline ?transposition_status ?oj_name ?oj_number ?oj_date ?oj_page ?expr_language ?title ?entry_into_force ?reference_commission ?date_notification ?website ?eli
    ORDER BY ?country ?nim_id
    

##Q13: ##########################################
### Not normalized query (not used in PROD): ####
## NIM Tab default
## sample <http://publications.europa.eu/resource/celex/32012L0027>
#################################################

prefix cdm: <http://publications.europa.eu/ontology/cdm#> 
prefix annotation: <http://publications.europa.eu/ontology/annotation#>
prefix owl: <http://www.w3.org/2002/07/owl#>

select distinct ?eli ?country ?nim_id ?transposition_deadline ?transposition_status ?oj_name ?oj_number ?oj_date ?oj_page ?expr_language ?title ?entry_into_force ?reference_commission ?date_notification 
{ select distinct ?eli ?country ?nim_id ?transposition_deadline ?transposition_status ?oj_name ?oj_number ?oj_date ?oj_page ?expr_language ?title ?entry_into_force ?reference_commission ?date_notification 
  where { <http://publications.europa.eu/resource/celex/32012L0027> ^owl:sameAs/owl:sameAs/cdm:work_date_document ?wdd. 
  OPTIONAL { <http://publications.europa.eu/resource/celex/32012L0027> cdm:resource_legal_eli ?eli.} 
  OPTIONAL { ?mne cdm:measure_national_implementing_implements_directive <http://publications.europa.eu/resource/celex/32012L0027>; cdm:measure_national_implementing_implemented_by_country ?country. 
  OPTIONAL { ?s annotation:transposition_deadline_transmitted ?transposition_deadline. ?s owl:annotatedSource ?mne. ?s owl:annotatedProperty cdm:measure_national_implementing_implements_directive. 
    ?s owl:annotatedTarget/^owl:sameAs/owl:sameAs <http://publications.europa.eu/resource/celex/32012L0027>. } 
  OPTIONAL {?mne cdm:measure_national_implementing_declaration_transposition_member-state ?transposition_status.} 
  OPTIONAL { ?mne ^owl:sameAs/owl:sameAs ?nim_id. FILTER (regex(?nim_id, '/resource/nim/')) ?mne ^owl:sameAs/owl:sameAs ?celex_id. FILTER (regex(?celex_id, '/resource/celex/')) } FILTER(!regex(?celex_id,'%2528.*%2529')) 
  OPTIONAL {?mne cdm:measure_national_implementing_name_official_journal ?oj_name.} 
  OPTIONAL {?mne cdm:measure_national_implementing_number_official_journal ?oj_number.} 
  OPTIONAL {?mne cdm:measure_national_implementing_date_official_journal ?oj_date.} 
  OPTIONAL {?mne cdm:measure_national_implementing_page_official_journal ?oj_page.} 
  OPTIONAL {?mne cdm:resource_legal_date_entry-into-force ?entry_into_force.} 
  OPTIONAL {?mne cdm:measure_national_implementing_reference_commission ?reference_commission.}
   OPTIONAL {?mne cdm:measure_national_implementing_date_notification ?date_notification.} ?exp cdm:expression_belongs_to_work ?mne; cdm:expression_uses_language ?expr_language; cdm:expression_title ?title. }}} ORDER BY ?country


##Q14: ##########################################
### Normalized query ( used in PROD): ###########
## List contains documents with titles ##########
## test with doc Celex 52011DC0666
## exple of ${language}= ENG, FRA, ITA, DEUS
#################################################

prefix cdm: <http://publications.europa.eu/ontology/cdm#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 

select distinct ?summary ?title where { ?sum owl:sameAs ?summary. filter (regex(?summary, '/resource/uriserv/')). 
OPTIONAL {?sum ^cdm:expression_belongs_to_work ?e. ?e cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/FRA>; cdm:expression_title ?title.} 
{ select DISTINCT ?sum where { ?s cdm:resource_legal_id_celex "52011DC0666"^^xsd:string; ^cdm:summary_summarizes_work ?sum. }} }


#################################################
## Q15: Not normalized query (not used):
## List contains documents with titles ##########
## test with doc Celex 52014SC0183
## exple of ${language}= ENG, FRA, ITA, DEUS
###################################################

prefix cdm: <http://publications.europa.eu/ontology/cdm#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>


Select ?summary ?title where { {select DISTINCT ?summary where {?s owl:sameAs/cdm:resource_legal_id_celex "52014SC0183"^^xsd:string;owl:sameAs/^cdm:summary_summarizes_work ?summary. } } 
OPTIONAL {?summary ^cdm:expression_belongs_to_work ?e. 
  ?e cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG>; cdm:expression_title ?title.}}
  
  
  
#################################################
## Q16: Query rectified applicable only in Test##
## test with doc Celex 62013TO0583
## exple of ${language}= ENG, FRA, ITA, DEUS
###################################################


prefix cdm: <http://publications.europa.eu/ontology/cdm#> 
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select ?date ?lang ?type ?celex ?rectificationOrderCelex 
where 
{ ?uri cdm:work_datetime_transmission ?date. 
  OPTIONAL{ 
  ?rectificationOrder cdm:resource_legal_corrects_resource_legal ?uri; cdm:resource_legal_id_celex ?rectificationOrderCelex. } 

  ?uri ^cdm:expression_belongs_to_work ?exp. 
  ?exp cdm:expression_uses_language ?lang;
^cdm:manifestation_manifests_expression ?manif. 
?manif cdm:manifestation_type ?type. 
?uri owl:sameAs ?celex. 
FILTER contains(str(?celex), "62013TO0583"). 
  
  VALUES ?uri {<http://publications.europa.eu/resource/cellar/6df75a83-c084-11e3-86f9-01aa75ed71a1> <http://publications.europa.eu/resource/cellar/3415a9e7-1d14-4017-95c1-aecec43462d5.0022>
  	   <http://publications.europa.eu/resource/cellar/771a3fe7-b701-438b-b8af-a94ed340fd63>}
} ORDER BY DESC(?date) ?uri ?lang ?type


##################################################
## Q17: Normalized query = not normalized query ##
## sample <http://publications.europa.eu/resource/celex/72004L0079HUN_28326> 
## sample <http://publications.europa.eu/resource/celex/71991L0687FRA_88000>
###################################################

prefix cdm: <http://publications.europa.eu/ontology/cdm#> 
prefix annotation: <http://publications.europa.eu/ontology/annotation#>
prefix owl: <http://www.w3.org/2002/07/owl#> 

select distinct ?country ?nim_id ?work_date_document ?transposition_deadline ?transposition_status ?oj_name ?oj_number ?oj_date ?oj_page 
?expr_language ?title ?entry_into_force ?reference_commission ?date_notification ?website ?eli (group_concat(distinct ?type;separator=", ") as
 ?types) where { ?cellar_id owl:sameAs ?nim_id. filter (regex(?nim_id,'resource/nim')).{ 
  select distinct ?country ?cellar_id ?work_date_document ?transposition_deadline ?transposition_status ?oj_name ?oj_number ?oj_date ?oj_page ?expr_language ?title ?entry_into_force ?reference_commission ?date_notification ?website ?eli ?type 
  where { ?cellar_id cdm:measure_national_implementing_implements_resource_legal/owl:sameAs? <http://publications.europa.eu/resource/celex/71991L0687FRA_88000>.?exp cdm:expression_belongs_to_work ?cellar_id ; cdm:expression_uses_language ?expr_language; cdm:expression_title ?title. 
  OPTIONAL {?manif cdm:manifestation_manifests_expression ?exp; cdm:manifestation_type ?type.} 
  OPTIONAL { ?cellar_id cdm:measure_national_implementing_implemented_by_country ?country.} 
  OPTIONAL { ?s annotation:transposition_deadline_transmitted ?transposition_deadline_transmitted. 
    OPTIONAL {?s annotation:transposition_notification ?date_notification.} ?s owl:annotatedSource ?cellar_id. 
    ?s owl:annotatedProperty cdm:measure_national_implementing_implements_resource_legal. 
    ?s owl:annotatedTarget/owl:sameAs? <http://publications.europa.eu/resource/celex/71991L0687FRA_88000>.} 
    bind((if(bound(?transposition_deadline_transmitted), ?transposition_deadline_transmitted,"1001-01-01")) as ?transposition_deadline). 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_declaration_transposition_member-state ?transposition_status.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_name_official_journal ?oj_name.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_number_official_journal ?oj_number.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_date_official_journal ?oj_date.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_page_official_journal ?oj_page.} 
    OPTIONAL {?cellar_id cdm:resource_legal_date_entry-into-force ?entry_into_force.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_reference_commission ?reference_commission.} 
    OPTIONAL {?cellar_id cdm:measure_national_implementing_national_website_link ?website.} 
    OPTIONAL {?cellar_id cdm:eli ?eli.} 
    OPTIONAL {?cellar_id cdm:work_date_document ?work_date_document.}}}} 
    GROUP BY ?country ?nim_id ?work_date_document ?transposition_deadline ?transposition_status ?oj_name ?oj_number ?oj_date ?oj_page 
?expr_language ?title ?entry_into_force ?reference_commission ?date_notification ?website ?eli
    ORDER BY ?country ?nim_id
    
##################################################
## Q18: Summaries of EU legislation ##
## Tree displayed with this SPARQL query #########
## Choose "agriculture" or 
## e.g., ${topicCode} = 010102
###################################################

prefix cdm: <http://publications.europa.eu/ontology/cdm#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 

select ?summary ?classification (xsd:integer(?obsolete) as ?obsoletes)
where { 
?sum cdm:summary_legislation_eu_is_about_classification_summary ?classification. 
FILTER (regex(?classification, 'http://publications.europa.eu/resource/authority/fd_700/010102')).
?sum ^cdm:expression_belongs_to_work ?expression.
?expression cdm:expression_uses_language ?language.
?sum cdm:summary_legislation_eu_obsolete ?obsolete.
?sum owl:sameAs ?summary.
filter (regex(?summary,'resource/uriserv')).
OPTIONAL{ ?expression cdm:expression_title ?title. }

} order by ?classification
  
 
 #######################################
##Q19: Public access procedure view ###
## Under development fonctionality ####
## sample <http://publications.europa.eu/resource/cellar/27afd6ef-dcc7-418f-97c4-acfc9126c6d4>
#######################################


prefix cdm: <http://publications.europa.eu/ontology/cdm#> 
prefix annotation: <http://publications.europa.eu/ontology/annotation#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

select distinct ?workflow ?minDate ?workflow_type ?document ?document_type ?document_title ?document_id ?document_date ?short_title ?legal_basis ?comment_on_legal_basis ?responsible_agent ?consults_agent ?consults_agent_type ?consultation_type ?consultation_duration ?event ?event_date ?event_type 
where{{select distinct ?workflow ?minDate 
  where 
  {{select (?predecessor as ?workflow) (MIN(xsd:date(?date)) AS ?minDate)
    {<http://publications.europa.eu/resource/cellar/27afd6ef-dcc7-418f-97c4-acfc9126c6d4>^owl:sameAs*/(^cdm:dossier_contains_work | cdm:resource_legal_produced_by_dossier) ?entry. ?entry a cdm:procedure_internal; cdm:dossier_is_logical_successor_of_dossier* ?predecessor. ?predecessor cdm:dossier_contains_event/cdm:event_date ?date.} group by ?predecessor} 
  UNION
{select(?following as ?workflow)(MIN(xsd:date(?date)) AS ?minDate){<http://publications.europa.eu/resource/cellar/27afd6ef-dcc7-418f-97c4-acfc9126c6d4>^owl:sameAs*/(^cdm:dossier_contains_work | cdm:resource_legal_produced_by_dossier) ?entry. ?entry a cdm:procedure_internal. ?following cdm:dossier_is_logical_successor_of_dossier+ ?entry; cdm:dossier_contains_event/cdm:event_date ?date.}group by ?following}}ORDER BY ?minDate} 
OPTIONAL {?workflow cdm:procedure_internal_has_type_procedure_internal ?workflow_type.} 
OPTIONAL {?workflow (cdm:dossier_contains_work | ^cdm:resource_legal_produced_by_dossier) ?document. ?document cdm:work_has_resource-type ?document_type
 OPTIONAL { ?exp cdm:expression_belongs_to_work ?document ; cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG> ; cdm:expression_subtitle ?document_title } ?document owl:sameAs ?document_id. ?document cdm:work_date_document ?document_date. 
 FILTER regex(?document_id, "resource/(celex|pi_com)")} 
 OPTIONAL {?workflow cdm:dossier_title ?short_title.} OPTIONAL{ ?workflow cdm:dossier_based_on_resource_legal/owl:sameAs ?legal_basis. 
  FILTER regex(?legal_basis, "resource/celex") ?lb annotation:comment_on_legal_basis ?comment_on_legal_basis. ?lb owl:annotatedSource ?workflow; owl:annotatedProperty cdm:dossier_based_on_resource_legal. ?lb owl:annotatedTarget/(owl:sameAs)? ?legal_basis} 
  OPTIONAL { ?workflow cdm:dossier_consults_agent/owl:sameAs ?consults_agent; cdm:dossier_consults_agent/rdf:type ?consults_agent_type 
    FILTER regex(?consults_agent, "resource/agent") FILTER regex(?consults_agent_type, "/ontology/cdm#(committee|expert-group)")} 
  OPTIONAL {?workflow cdm:procedure_internal_has_type_consultation ?consultation_type.} 
  OPTIONAL {?workflow cdm:dossier_duration ?consultation_duration.} 
  OPTIONAL {?workflow cdm:dossier_contains_event ?event. ?event cdm:event_date ?event_date; cdm:event_has_type_concept_type_event ?event_type; cdm:event_responsibility_of_institution ?responsible_agent}}

##################################
## Under DEV functionality    ####
## Q20: legal basis procedure #####
###################################

prefix cdm: <http://publications.europa.eu/ontology/cdm#> 
prefix annotation: <http://publications.europa.eu/ontology/annotation#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

select * where { VALUES ?basic_act {<http://publications.europa.eu/resource/cellar/27afd6ef-dcc7-418f-97c4-acfc9126c6d4>} 
?basic_act ^cdm:dossier_based_on_resource_legal ?workflow. ?workflow cdm:dossier_responsibility_of_agent <http://publications.europa.eu/resource/authority/corporate-body/COM>. 
OPTIONAL {?workflow cdm:dossier_title ?short_title.} 
OPTIONAL { ?workflow cdm:dossier_contains_work ?work. ?work owl:sameAs ?work_id. 
  FILTER regex(?work_id, "resource/pi_com") ?work cdm:work_has_resource-type ?type. 
FILTER (?type in ( <http://publications.europa.eu/resource/authority/resource-type/DEC_DEL_DRAFT>, <http://publications.europa.eu/resource/authority/resource-type/DEC_IMPL_DRAFT>, <http://publications.europa.eu/resource/authority/resource-type/DIR_DEL_DRAFT>, 
  <http://publications.europa.eu/resource/authority/resource-type/DIR_IMPL_DRAFT>, <http://publications.europa.eu/resource/authority/resource-type/REG_DEL_DRAFT>, <http://publications.europa.eu/resource/authority/resource-type/REG_IMPL_DRAFT>, <http://publications.europa.eu/resource/authority/resource-type/DEC_DRAFT>, 
  <http://publications.europa.eu/resource/authority/resource-type/DIR_DRAFT>, <http://publications.europa.eu/resource/authority/resource-type/REG_DRAFT>))}}




Queries Category 2
===================

Q1
===
DEFINE input:inference "cdm_rule_set"
#DEFINE input:same-as "yes"
prefix cdm: <http://publications.europa.eu/ontology/cdm#>
select  (count (distinct ?lr ) as ?total) 
 where {
?lr cdm:work_date_creation_legacy ?dat_cre.
?lr a cdm:resource_legal .
FILTER (!regex(str(?dat_cre), "^2016-"))
}

Q2
===
#Q2:Total number of authors for these resource_legal:
# BGP: 1
 
#DEFINE input:inference "cdm_rule_set"
prefix cdm: <http://publications.europa.eu/ontology/cdm#>
SELECT (COUNT(*) AS ?count) where{
select  distinct ?lr ?author 
 where {
?lr cdm:work_created_by_agent ?author.
?lr cdm:work_date_creation_legacy ?dat_cre.
?lr a cdm:resource_legal .
FILTER (!regex(str(?dat_cre), "^2016-"))
}
}

Q3
===
#Q3:Repartition of the authors for documents published in the OJ or 
# not published in the OJ (that query was working beginning of #this year,
# not anymore now, so that one would definitively be worth looking at!!)
# BGP: 2

#DEFINE input:inference "cdm_rule_set"
prefix cdm: <http://publications.europa.eu/ontology/cdm#>
select  (count(distinct(?lr)) as ?lrTotal) ?author ?boolOJ  
 where {
?lr cdm:work_created_by_agent ?author.
?lr cdm:work_date_creation_legacy ?dat_cre.
?lr a cdm:resource_legal .
OPTIONAL
{?lr cdm:resource_legal_published_in_official-journal ?oj.}
bind((if(bound(?oj), "1","0")) as ?boolOJ).
FILTER (!regex(str(?dat_cre), "^2016-"))
} group by ?author ?boolOJ

Q4
===
#Q4:Query for setting the inforce flag ($systemdate replaced by the current date)
# s/'{$systemdate}'/"2017-03-01"
# BGP: 5

PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT * 
WHERE 
  { 
?w cdm:resource_legal_date_entry-into-force ?date_begin.
FILTER(xsd:date(?date_begin) > "2017-03-01"^^<http://www.w3.org/2001/XMLSchema#date>||xsd:date(?date_begin) = '1001-01-01'^^<http://www.w3.org/2001/XMLSchema#date>)
FILTER not exists {?w cdm:resource_legal_date_entry-into-force ?date_begin2.
FILTER(xsd:date(?date_begin2) <= "2017-03-01'"^^<http://www.w3.org/2001/XMLSchema#date>)
FILTER(xsd:date(?date_begin2) != '1001-01-01'^^<http://www.w3.org/2001/XMLSchema#date>)
}
FILTER not exists {?m cdm:manifestation_official-journal_part_durability 'EPH'^^<http://www.w3.org/2001/XMLSchema#string>.
?m cdm:manifestation_manifests_expression ?e.
?e cdm:expression_belongs_to_work ?w.
}
?w cdm:resource_legal_date_end-of-validity ?date_end.
FILTER(xsd:date(?date_end) >= "2017-03-01"^^<http://www.w3.org/2001/XMLSchema#date>)
OPTIONAL{?w cdm:resource_legal_id_celex ?celex.}
MINUS
   { ?w cdm:resource_legal_in-force ?inforce.
FILTER(xsd:date(?inforce)="false"^^<http://www.w3.org/2001/XMLSchema#boolean>)
}
}

Q5
===
#Q5: Another Query for setting the inforce flag
# s/{$systemdate}/2007-03-01
# BGP: 5

PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
SELECT * WHERE { 
?s cdm:resource_legal_date_end-of-validity ?date_end.
FILTER not exists {?s cdm:resource_legal_date_end-of-validity ?date_end2.
FILTER(str(?date_end2) >= '2017-03-01' ) }
FILTER(str(?date_end) < '2017-03-01' )
FILTER(str(?date_end) > '1003-03-03' )
OPTIONAL {?s cdm:resource_legal_has_type_act_concept_type_act ?typeact.}
FILTER not exists {?m cdm:manifestation_official-journal_part_durability 'EPH'^^<http://www.w3.org/2001/XMLSchema#string>.?m cdm:manifestation_manifests_expression ?e.?e cdm:expression_belongs_to_work ?s.}
OPTIONAL{?s cdm:resource_legal_id_celex ?celex.}
MINUS { ?s cdm:resource_legal_in-force ?inforce. FILTER(?inforce="false" ^^<http://www.w3.org/2001/XMLSchema#boolean>) }
 }
 
 Q6
 ===
 #Q6: Query3 for setting the inforce flag
# BGP: 4

PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT * 
WHERE 
  { ?s cdm:resource_legal_date_entry-into-force ?date_begin;
cdm:resource_legal_date_end-of-validity ?date_end.
FILTER(xsd:date(?date_begin) != '1001-01-01'^^<http://www.w3.org/2001/XMLSchema#date>)
FILTER(xsd:date(?date_begin) <= '2017-03-01'^^<http://www.w3.org/2001/XMLSchema#date>)
FILTER(xsd:date(?date_end) > '2017-03-01'^^<http://www.w3.org/2001/XMLSchema#date>)
FILTER not exists {?m cdm:manifestation_official-journal_part_durability 'EPH'^^<http://www.w3.org/2001/XMLSchema#string>.?m cdm:manifestation_manifests_expression ?e.?e cdm:expression_belongs_to_work ?s.}
OPTIONAL{?s cdm:resource_legal_id_celex ?celex.}
MINUS
   { ?s cdm:resource_legal_in-force ?inforce.
FILTER(?inforce="true" ^^<http://www.w3.org/2001/XMLSchema#boolean>)
} 
}

Q7
===
#Q7: Query for setting the adopted flag
# BGP: 5

#DEFINE input:inference "cdm_rule_set"
PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
SELECT ?dossier ?date1 ?date2
WHERE 
  { ?dossier cdm:dossier_contains_event_legal ?event1.
OPTIONAL {?dossier cdm:dossier_adopted-proposal ?adopted.}
?event1 cdm:event_has_type_concept_type_event ?eventType1.
?event1 cdm:event_date ?date1.
FILTER ( 
?eventType1=<http://publications.europa.eu/resource/authority/event/DEC_byCOUN_ASS>
||?eventType1=<http://publications.europa.eu/resource/authority/event/DEC_byCMT_CONJOIN>
||?eventType1=<http://publications.europa.eu/resource/authority/event/DEC_byACP_MIN>
||?eventType1=<http://publications.europa.eu/resource/authority/event/DEC_byACP_AMB>
||?eventType1=<http://publications.europa.eu/resource/authority/event/DEC_byCMT_COOP>
||?eventType1=<http://publications.europa.eu/resource/authority/event/DEC_byCOUN_JOIN>
||?eventType1=<http://publications.europa.eu/resource/authority/event/SIGN_PAR_byEP_CONSIL>
||?eventType1=<http://publications.europa.eu/resource/authority/event/ADP_FRM_byCONSIL>
||?eventType1=<http://publications.europa.eu/resource/authority/event/ADP_PAR_byCONSIL>
||?eventType1=<http://publications.europa.eu/resource/authority/event/SIGN_byEP_CONSIL>
||?eventType1=<http://publications.europa.eu/resource/authority/event/DEC_byCOUN_COOP>
||?eventType1=<http://publications.europa.eu/resource/authority/event/DEC_byCMT_JOIN>
||?eventType1=<http://publications.europa.eu/resource/authority/event/ADP_ACT_byCOM>
) 
?event1 cdm:event_legal_contains_work ?adoptedWork1.
FILTER (regex(str(?adoptedWork1),'celex/[234][0-9][0-9][0-9][0-9][RLDSFE]'))
OPTIONAL{ 
?dossier cdm:dossier_contains_event_legal ?event2.
?event2 cdm:event_has_type_concept_type_event ?eventType2.
?event2 cdm:event_date ?date2. 
FILTER ( 
?eventType2=<http://publications.europa.eu/resource/authority/event/DEC_byCOUN_ASS>
||?eventType2=<http://publications.europa.eu/resource/authority/event/DEC_byCMT_CONJOIN>
||?eventType2=<http://publications.europa.eu/resource/authority/event/DEC_byACP_MIN>
||?eventType2=<http://publications.europa.eu/resource/authority/event/DEC_byACP_AMB>
||?eventType2=<http://publications.europa.eu/resource/authority/event/DEC_byCMT_COOP>
||?eventType2=<http://publications.europa.eu/resource/authority/event/DEC_byCOUN_JOIN>
||?eventType2=<http://publications.europa.eu/resource/authority/event/SIGN_PAR_byEP_CONSIL>
||?eventType2=<http://publications.europa.eu/resource/authority/event/ADP_FRM_byCONSIL>
||?eventType2=<http://publications.europa.eu/resource/authority/event/ADP_PAR_byCONSIL>
||?eventType2=<http://publications.europa.eu/resource/authority/event/SIGN_byEP_CONSIL>
||?eventType2=<http://publications.europa.eu/resource/authority/event/DEC_byCOUN_COOP>
||?eventType2=<http://publications.europa.eu/resource/authority/event/DEC_byCMT_JOIN>
||?eventType2=<http://publications.europa.eu/resource/authority/event/ADP_ACT_byCOM>
) 
?event2 cdm:event_legal_contains_work ?adoptedWork2.
FILTER (regex(str(?adoptedWork2),'celex/[234][0-9][0-9][0-9][0-9][RLDSFE]'))
} 
 MINUS 
{ 
?dossier cdm:dossier_adopted-proposal ?adopted.
FILTER(?adopted="true" ^^<http://www.w3.org/2001/XMLSchema#boolean>)
?dossier cdm:dossier_date_adopted ?dateadopted.
} 
}

Q8
===
#########################################
#Q8: Query for setting the pending flag
#BGP: 4
#########################################
 
#DEFINE input:inference "cdm_rule_set"
prefix cdm: <http://publications.europa.eu/ontology/cdm#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select distinct ?dossier where
{
?dossier_cellar owl:sameAs ?dossier.
FILTER(regex(str(?dossier),'procedure'))
?dossier_cellar a cdm:dossier.
?dossier_cellar a cdm:procedure_code_interinstitutional.
?dossier_cellar cdm:dossier_initiated_by_act_preparatory ?work.
?event cdm:event_legal_contains_work ?work.
?event (cdm:event_date|cdm:event_legal_date) ?date.
FILTER(?date >= '2015-06-29' ^^<http://www.w3.org/2001/XMLSchema#date>)
MINUS
{
?dossier_cellar cdm:dossier_withdrawn-proposal ?withdrawn.
FILTER(?withdrawn="true"^^xsd:boolean)
}
MINUS
{
?dossier_cellar cdm:dossier_adopted-proposal ?adopted.
FILTER(?adopted="true"^^xsd:boolean)
}
MINUS
{
?dossier_cellar cdm:dossier_pending-proposal ?pending.
FILTER(?pending="true"^^xsd:boolean)
}
}

Q9
===
#Q9: Query for selecting work and dossiers to analyse
#BGP: 1
 
prefix cdm: <http://publications.europa.eu/ontology/cdm#> 
prefix owl: <http://www.w3.org/2002/07/owl#>
select distinct
?Cellar_Id 
?Work_Identifier 
?dossierId
?legal_basis_celex
?legal_basis_celex_comment
?legal_basis_eli
?legal_basis_eli_subdivision
?new_ceres_tz
?eurlex_tz
?new_ceres_pz
?eurlex_pz
where {?Cellar_Id cdm:work_has_resource-type ?type.
?Cellar_Id owl:sameAs ?Work_Identifier.
?Cellar_Id cdm:work_id_document ?id_doc.
?dossier cdm:dossier_contains_work ?Cellar_Id.
?dossier cdm:dossier_identifier ?dossierId_.
?dossier cdm:procedure_internal_has_type_procedure_internal <http://publications.europa.eu/resource/authority/com_internal_procedure/ISC_WORKFLOW>.
BIND(str(?dossierId_) as ?dossierId)
FILTER(regex(?Work_Identifier,'pi_com'))
FILTER(regex(?dossierId,'publicAccess'))
FILTER not exists{?Cellar_Id cdm:resource_legal_based_on_resource_legal ?legal_basis_.}
BIND(str(?legal_basis_) as ?legal_basis)
BIND(str(?legal_basis_) as ?legal_basis_comment)
BIND(str(?legal_basis_) as ?legal_basis_eli)
BIND(str(?legal_basis_) as ?legal_basis_eli_subdivision)
FILTER(regex(str(?type),'DRAFT$'))
BIND(concat("http://newceres-test/ceres-monitoring/publicationview?type=businessId&value=",replace(str(?Work_Identifier),"(^.+immc[/])(.+$)","$2"),"&originFamily=ALL&fromExecutionDate=&toExecutionDate=&destinationGroup=ALL&destinationIdentifier=ALL&workflowOverallStatus=ALL&refreshTime=0&submit_button=Submit") as ?new_ceres_tz)
BIND(concat("http://eurlex-active-test/eurlex-frontoffice/legal-content/EN/ALL/?uri=immc:",encode_for_uri(replace(?id_doc,'pi_com:',''))) as ?eurlex_tz)
BIND(concat("http://newceres/ceres-monitoring/publicationview?type=businessId&value=",replace(str(?Work_Identifier),"(^.+immc[/])(.+$)","$2"),"&originFamily=ALL&fromExecutionDate=&toExecutionDate=&destinationGroup=ALL&destinationIdentifier=ALL&workflowOverallStatus=ALL&refreshTime=0&submit_button=Submit") as ?new_ceres_pz)
BIND(concat("http://eur-lex.europa.eu/legal-content/EN/ALL/?uri=immc:",encode_for_uri(replace(?id_doc,'pi_com:',''))) as ?eurlex_pz)
}

Q10
====
##################################################
#Q10: source--> query_vrep.sparql
# email: 01/12/2016
# sample error in Stardog or with qparse of JENA: Variable 'FIELD_DN' in projection is not present in GROUP BY
# BGP: 15
##################################################

prefix cdm: <http://publications.europa.eu/ontology/cdm#>
prefix cmr: <http://publications.europa.eu/ontology/cdm/cmr#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
select distinct 
?FIELD_DN 
(group_concat(distinct ?resourceTypeLabel;separator="#") as ?FIELD_FM) 
?FIELD_SO_COLL
?FIELD_SO_NUM
?FIELD_SO_PAGE_FIRST
?FIELD_SO_PAGE_LAST
?FIELD_SO_PAGE_TOTAL
?FIELD_PD
?FIELD_DD
(group_concat(distinct ?if; separator="#") as ?FIELD_IF)
(group_concat(distinct ?vv; separator="#") as ?FIELD_EV)
(group_concat(distinct ?LegalBasisCelex; separator="#") as ?FIELD_LB)
?repeals
?repealedBy
?repealsImplicit
?repealedByImplicit
(group_concat(distinct ?directoryCode; separator="#") as ?FIELD_CC)
?FIELD_DT_SEC
?FIELD_DT_TYP
?FIELD_DT_ANN
#?FIELD_DT_CORR
#?FIELD_DT_CORR2
?FIELD_UPD_T
?FIELD_TI
?FIELD_REP
where 
{ ?act cdm:resource_legal_id_sector ?sector. 
FILTER (
      ?sector="2"^^<http://www.w3.org/2001/XMLSchema#string>||
      ?sector="3"^^<http://www.w3.org/2001/XMLSchema#string>||
      ?sector="4"^^<http://www.w3.org/2001/XMLSchema#string>
      ) 
?act cdm:resource_legal_in-force "true"^^<http://www.w3.org/2001/XMLSchema#boolean>. 
?act cdm:resource_legal_repertoire ?FIELD_REP.
FILTER (
      ?FIELD_REP="REP"^^<http://www.w3.org/2001/XMLSchema#string>||
      ?FIELD_REP="LIE"^^<http://www.w3.org/2001/XMLSchema#string>
      ) 
?act cdm:resource_legal_id_celex ?FIELD_DN.
OPTIONAL {
      ?act cdm:work_has_resource-type/skos:prefLabel ?resourceTypeLabel.
      FILTER (lang(?resourceTypeLabel)="en").
      }
OPTIONAL {
      ?act cdm:resource_legal_published_in_official-journal ?oj.
      ?oj cdm:official-journal_part_of_collection_document ?col.
      BIND(substr(str(?col),70) as ?FIELD_SO_COLL).
      ?oj cdm:official-journal_number ?FIELD_SO_NUM.
      ?oj cdm:work_date_document ?FIELD_PD.
      ?act cdm:work_date_document ?FIELD_DD.
      }
OPTIONAL {
       ?act cdm:resource_legal_date_entry-into-force ?if.
       }
OPTIONAL {
       ?act cdm:resource_legal_date_end-of-validity ?vv.
       }
OPTIONAL {
      ?act cdm:resource_legal_based_on_resource_legal ?LegalBasis.
      ?LegalBasis cdm:resource_legal_id_celex ?LegalBasisCelex.
      }
OPTIONAL{
      ?act cdm:resource_legal_repeals_resource_legal ?repeals_.
      ?repeals_ cdm:resource_legal_id_celex ?repeals.
      }
OPTIONAL{
      ?repealedBy_ cdm:resource_legal_repeals_resource_legal ?act.
      ?repealedBy_ cdm:resource_legal_id_celex ?repealedBy.
      }
OPTIONAL{
      ?act cdm:resource_legal_implicitly_repeals_resource_legal ?repealsImplicit_.
      ?repealsImplicit_ cdm:resource_legal_id_celex ?repealsImplicit.
      }
OPTIONAL{
      ?repealedByImplicit_ cdm:resource_legal_implicitly_repeals_resource_legal ?act.
      ?repealedByImplicit_ cdm:resource_legal_id_celex ?repealedByImplicit.
      }
OPTIONAL{
      ?act cdm:resource_legal_is_about_concept_directory-code ?cc.
      BIND(substr(xsd:string(?cc),57) as ?directoryCode).
      }
?act cdm:resource_legal_id_sector ?FIELD_DT_SEC.
?act cdm:resource_legal_type ?FIELD_DT_TYP.
?act cdm:resource_legal_year ?FIELD_DT_ANN.
#OPTIONAL{?act cdm:resource_legal_number_corrigendum ?FIELD_DT_CORR.}
?act cmr:lastModificationDate ?FIELD_UPD_T.
?exp cdm:expression_belongs_to_work ?act.
?exp cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG>.
?exp cdm:expression_title ?FIELD_TI.
OPTIONAL{
      ?manif cdm:manifestation_manifests_expression ?exp.
      ?manif cdm:manifestation_official-journal_part_page_first ?FIELD_SO_PAGE_FIRST.
      OPTIONAL{?manif cdm:manifestation_official-journal_part_page_last ?FIELD_SO_PAGE_LAST.}
      OPTIONAL{?manif cdm:manifestation_official-journal_part_pages_total ?FIELD_SO_PAGE_TOTAL.}
#     OPTIONAL{?manif cdm:manifestation_official-journal_part_is_corrigendum_printer ?FIELD_DT_CORR2}
      }
} GROUP by ?FIELD_DN ?FIELD_SO_COLL ?FIELD_SO_NUM ?FIELD_SO_PAGE_FIRST ?FIELD_SO_PAGE_LAST ?FIELD_SO_PAGE_TOTAL ?FIELD_PD ?FIELD_DD ?repeals ?repealedBy ?repealsImplicit ?repealedByImplicit ?FIELD_DT_SEC ?FIELD_DT_TYP ?FIELD_DT_ANN ?FIELD_UPD_T ?FIELD_TI ?FIELD_REP


Q11
===
#######################################
#Q11: source--> query_sec5.sparql
# email: 01/12/2016
# BGP: 16
##########################################
prefix cdm: <http://publications.europa.eu/ontology/cdm#>
prefix cmr: <http://publications.europa.eu/ontology/cdm/cmr#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>

 select distinct ?FIELD_DN 
  (group_concat(distinct ?resourceTypeLabel;separator="#") as ?FIELD_FM) 
?FIELD_SO_COLL
?FIELD_SO_NUM
?FIELD_SO_PAGE_FIRST
?FIELD_SO_PAGE_LAST
?FIELD_SO_PAGE_TOTAL
?FIELD_PD
?FIELD_DD
(group_concat(distinct ?if; separator="#") as ?FIELD_IF)
(group_concat(distinct ?vv; separator="#") as ?FIELD_EV)
(group_concat(distinct ?LegalBasisCelex; separator="#") as ?FIELD_LB)
?repeals
?repealedBy
?repealsImplicit
?repealedByImplicit
(group_concat(distinct ?directoryCode; separator="#") as ?FIELD_CC)
?FIELD_DT_SEC
?FIELD_DT_TYP
?FIELD_DT_ANN
?FIELD_UPD_T
?FIELD_TI
?FIELD_REP
where 
{ 
?dossier cdm:dossier_pending-proposal "true"^^<http://www.w3.org/2001/XMLSchema#boolean>. 
?dossier cdm:dossier_initiated_by_act_preparatory ?act.  
OPTIONAL{?act cdm:resource_legal_id_sector ?sector.}
OPTIONAL{?act cdm:resource_legal_repertoire ?FIELD_REP.}
?act cdm:resource_legal_id_celex ?FIELD_DN.
?act cdm:work_has_resource-type/skos:prefLabel ?resourceTypeLabel.
FILTER (lang(?resourceTypeLabel)="en").
OPTIONAL{
       ?act cdm:resource_legal_published_in_official-journal ?oj.
       ?oj cdm:official-journal_part_of_collection_document ?col.
       ?oj cdm:official-journal_number ?FIELD_SO_NUM.
       ?oj cdm:work_date_document ?FIELD_PD.
       ?act cdm:work_date_document ?FIELD_DD.
       }
BIND(substr(str(?col),70) as ?FIELD_SO_COLL).
OPTIONAL {
       ?act cdm:resource_legal_date_entry-into-force ?if.
       }
OPTIONAL {
       ?act cdm:resource_legal_date_end-of-validity ?vv.
       }
OPTIONAL {
      ?act cdm:resource_legal_based_on_resource_legal ?LegalBasis.
      ?LegalBasis cdm:resource_legal_id_celex ?LegalBasisCelex.
      }
OPTIONAL{
      ?act cdm:resource_legal_repeals_resource_legal ?repeals_.
      ?repeals_ cdm:resource_legal_id_celex ?repeals.
      }
OPTIONAL{
      ?repealedBy_ cdm:resource_legal_repeals_resource_legal ?act.
      ?repealedBy_ cdm:resource_legal_id_celex ?repealedBy.
      }
OPTIONAL{
      ?act cdm:resource_legal_implicitly_repeals_resource_legal ?repealsImplicit_.
      ?repealsImplicit_ cdm:resource_legal_id_celex ?repealsImplicit.
      }
OPTIONAL{
      ?repealedByImplicit_ cdm:resource_legal_implicitly_repeals_resource_legal ?act.
      ?repealedByImplicit_ cdm:resource_legal_id_celex ?repealedByImplicit.
      }
OPTIONAL{?act cdm:resource_legal_is_about_concept_directory-code ?cc.}
BIND(substr(xsd:string(?cc),57) as ?directoryCode).
?act cdm:resource_legal_id_sector ?FIELD_DT_SEC.
?act cdm:resource_legal_type ?FIELD_DT_TYP.
?act cdm:resource_legal_year ?FIELD_DT_ANN.
?act cmr:lastModificationDate ?FIELD_UPD_T.
?exp cdm:expression_belongs_to_work ?act.
?exp cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG>.
?exp cdm:expression_title ?FIELD_TI.
OPTIONAL{
      ?manif cdm:manifestation_manifests_expression ?exp.
      ?manif cdm:manifestation_official-journal_part_page_first ?FIELD_SO_PAGE_FIRST.
      OPTIONAL{?manif cdm:manifestation_official-journal_part_page_last ?FIELD_SO_PAGE_LAST.}
      OPTIONAL{?manif cdm:manifestation_official-journal_part_pages_total ?FIELD_SO_PAGE_TOTAL.}

      }
} group by ?FIELD_DN ?FIELD_SO_COLL ?FIELD_SO_NUM ?FIELD_SO_PAGE_FIRST ?FIELD_SO_PAGE_LAST ?FIELD_SO_PAGE_TOTAL ?FIELD_PD ?FIELD_DD ?repeals ?repealedBy ?repealsImplicit ?repealedByImplicit ?FIELD_DT_SEC ?FIELD_DT_TYP ?FIELD_DT_ANN ?FIELD_UPD_T ?FIELD_TI ?FIELD_REP

Q12
===
#####################################
# Q12: source--> query_vrcc.sparql
# email: 01/12/2016
# BGP: 2
######################################

prefix cdm: <http://publications.europa.eu/ontology/cdm#> 
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#>
select 
?FIELD_DN 
(group_concat(distinct ?directoryCode; separator="#") as ?FIELD_CC)
where 
{
?act cdm:resource_legal_id_celex ?FIELD_DN. 
?act cdm:resource_legal_id_sector ?sector. 
FILTER (
      ?sector="2"^^<http://www.w3.org/2001/XMLSchema#string>||
      ?sector="3"^^<http://www.w3.org/2001/XMLSchema#string>||
      ?sector="4"^^<http://www.w3.org/2001/XMLSchema#string>||
      ?sector="5"^^<http://www.w3.org/2001/XMLSchema#string>
      ) 
OPTIONAL{
      ?act cdm:resource_legal_is_about_concept_directory-code ?cc.
      BIND(substr(xsd:string(?cc),57) as ?directoryCode).
      }
} group by ?FIELD_DN
order by ?FIELD_DN

Q13
===
#########################################
##Q13 --> dynamicQuery_modified.sparql ##
##BGP: 6
#########################################

PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX eurovoc: <http://eurovoc.europa.eu/schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
SELECT ?work 
       (?abstract as ?abstracts)
       (group_concat(distinct ?title_;separator=", ") as ?title)  
       ?date
      (group_concat(distinct ?workId;separator=", ") as ?workIds) 
      (group_concat(distinct ?workType;separator=", ") as ?workTypes) 
      (group_concat(distinct ?subject;separator=", ") as ?subjects) 
      (group_concat(distinct ?author;separator="; ") as ?authors)
      (group_concat(distinct ?work;separator=", ") as ?cellarURIs)
      (group_concat(distinct ?mtype;separator=", ") as ?mtypes)WHERE {
   ?work cdm:work_is_about_concept_eurovoc/dc:identifier ?subject;
   cdm:work_id_document ?workId;
   rdf:type ?workType.
  OPTIONAL{

{
{?work cdm:work_created_by_agent/dc:identifier ?author.}
UNION
{?work cdm:work_created_by_agent/cdm:agent_name ?author.}
}

UNION
{
{?work cdm:work_authored_by_agent/dc:identifier ?author.}
UNION
{?work cdm:work_authored_by_agent/cdm:agent_name ?author.}
}

UNION
{ 
{?work cdm:work_illustrated_by_agent/dc:identifier ?author.}
UNION 
{?work cdm:work_illustrated_by_agent/cdm:agent_name ?author.}
}

UNION
{
{?work cdm:work_compiled_by_agent/dc:identifier ?author.}
UNION
{?work cdm:work_compiled_by_agent/cdm:agent_name ?author.}
}

UNION
{
{?work cdm:work_contributed_to_by_agent/dc:identifier ?author.}
UNION
{?work cdm:work_contributed_to_by_agent/cdm:agent_name ?author.}
}

UNION
{
{?work cdm:work_edited_by_agent/dc:identifier ?author.}
UNION
{?work cdm:work_edited_by_agent/cdm:agent_name ?author.}
}

UNION
{
{?work cdm:work_translated_by_agent/dc:identifier ?author.}
UNION
{?work cdm:work_translated_by_agent/cdm:agent_name ?author.}
}

}
   ?expr cdm:expression_belongs_to_work ?work.
   optional {?expr cdm:expression_title ?title_;
                   cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG>.}
   optional {?expr cdm:expression_abstract ?abstract}.
   optional {?m cdm:manifestation_manifests_expression ?expr; cdm:manifestation_type ?mtype}
{
   SELECT DISTINCT ?work ?date WHERE {
?work cdm:work_is_about_concept_eurovoc/dc:identifier "3493";
      cdm:work_date_document ?date.
?expr2 cdm:expression_belongs_to_work ?work.
 ?expr2 cdm:expression_title ?title2;
      cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG>. 
OPTIONAL{

{?work cdm:work_created_by_agent/dc:identifier "EC".}
UNION
{?work cdm:work_authored_by_agent/dc:identifier "EC".}
UNION 
{?work cdm:work_illustrated_by_agent/dc:identifier "EC".}
UNION 
{?work cdm:work_compiled_by_agent/dc:identifier "EC".}
UNION
{?work cdm:work_contributed_to_by_agent/dc:identifier "EC".}
UNION
{?work cdm:work_edited_by_agent/dc:identifier "EC".}
UNION
{?work cdm:work_translated_by_agent/dc:identifier "EC".}
}
}
ORDER BY DESC(?date)
OFFSET 0
LIMIT 10 }
}
GROUP BY ?work ?date ?abstract

Q14
===
