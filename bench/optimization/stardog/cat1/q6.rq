###############################
##Q6: Dynamic Query ###########
###############################

## updated 2022: dc:identifier "EP" 
PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX eurovoc: <http://eurovoc.europa.eu/schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
SELECT ?work 
       ((?abstract) as ?abstracts) 
       (group_concat(distinct ?title_;separator=", ") as ?title)  
       ?date
      (group_concat(distinct ?workId;separator=", ") as ?workIds) 
      (group_concat(distinct ?workType;separator=", ") as ?workTypes) 
      (group_concat(distinct ?subject;separator=", ") as ?subjects) 
      (group_concat(distinct ?author;separator=", ") as ?authors)
      (group_concat(distinct ?work;separator=", ") as ?cellarURIs)
      (group_concat(distinct ?mtype;separator=", ") as ?mtypes)WHERE {


#pragma join.choice.strategy streaming

   ?work cdm:work_is_about_concept_eurovoc/dc:identifier ?subject;
   cdm:work_id_document ?workId;
   rdf:type ?workType;
   cdm:work_created_by_agent/dc:identifier ?author.
   ?expr cdm:expression_belongs_to_work ?work.


{
   SELECT DISTINCT ?work ?date WHERE {
?work cdm:work_is_about_concept_eurovoc/dc:identifier "3493";
      cdm:work_date_document ?date.
?expr2 cdm:expression_belongs_to_work ?work.
 ?expr2 cdm:expression_title ?title2;
      cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG>. 
?work cdm:work_created_by_agent/dc:identifier "EP".
}
ORDER BY DESC(?date)
OFFSET 0
LIMIT 10 }


   optional {?expr cdm:expression_title ?title_;
                   cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG>.}
   optional {?expr cdm:expression_abstract ?abstract}.
   optional {?m cdm:manifestation_manifests_expression ?expr; cdm:manifestation_type ?mtype}

}
GROUP BY ?work ?date ?abstract