#################################################
## Q15
## List contains documents with titles ##########
## test with doc Celex 32013D0752
## example of ${language}= ENG, FRA, ITA, DEUS
## BGP: 1
###################################################

# Updates: Removed owl:sameAs path and just replaced by single properties
# cdm:resource_legal_id_celex and cdm:summary_summarizes_work to make query match again

prefix cdm: <http://publications.europa.eu/ontology/cdm#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>


select ?summary ?title where {
	{	
		select DISTINCT ?summary where {
			?s cdm:resource_legal_id_celex "32013D0752"^^xsd:string;
				^cdm:summary_summarizes_work ?summary. } 
	} 
	OPTIONAL {
		?summary ^cdm:expression_belongs_to_work ?e. 
		?e cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/ENG>; cdm:expression_title ?title.
	}
}

