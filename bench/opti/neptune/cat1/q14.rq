PREFIX hint: <http://aws.amazon.com/neptune/vocab/v01/QueryHints#>
prefix cdm: <http://publications.europa.eu/ontology/cdm#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 

select distinct ?summary ?title where { 
  hint:Group hint:joinOrder "Ordered" . 

  { 
    select DISTINCT ?sum where { 
      ?s cdm:resource_legal_id_celex "52011DC0666"^^xsd:string; ^cdm:summary_summarizes_work ?sum. 
    }
  } 
  ?sum owl:sameAs ?summary. 
  filter (regex(STR(?summary), '/resource/uriserv/')). 
  OPTIONAL {
    ?sum ^cdm:expression_belongs_to_work ?e . 
    ?e cdm:expression_uses_language <http://publications.europa.eu/resource/authority/language/FRA>; cdm:expression_title ?title.
  } 
}
