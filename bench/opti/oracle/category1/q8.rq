#################################################################
## Q8: Search by subject - author faceted applied-2 selections ##
#################################################################

PREFIX ORACLE_SEM_FS_NS:   
<http://oracle.com/semtech#timeout=60,degree=8,ods=6>

## Oracle optimization 
## removing distinct in group_concat and moving order by to same block than LIMIT

# Turn off some special character escaping  
PREFIX ORACLE_SEM_HT_NS: <http://oracle.com/semtech#ESCAPE_STR_FUNC=F,DISABLE_REGEX_UNESC>


PREFIX cdm: <http://publications.europa.eu/ontology/cdm#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX eurovoc: <http://eurovoc.europa.eu/schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
SELECT ?work 
      ?expr
      (?lang1 as ?lang) 
      (group_concat(distinct ?title_;separator=", ") as ?title)  
      ?date
      (?abstract as ?abstracts )
      (group_concat(STR(?workId);separator=", ") as ?workIds) 
      (group_concat(STR(?workType);separator=", ") as ?workTypes) 
      (group_concat(STR(?subject);separator=", ") as ?subjects) 
      (group_concat(STR(?author);separator=", ") as ?authors)
      (group_concat(STR(?work);separator=", ") as ?cellarURIs)
      (group_concat(STR(?mtype);separator=", ") as ?mtypes) 
      (group_concat(STR(?thumbnail);separator=", ") as ?thumbnails)
WHERE {
      ?work cdm:work_id_document ?workId;
         rdf:type ?workType.
      optional{?work cdm:work_is_about_concept_eurovoc/dc:identifier ?subject.}
      optional{?work cdm:work_created_by_agent/dc:identifier ?author.}
      ?expr cdm:expression_belongs_to_work ?work.
      optional {?expr cdm:expression_abstract ?abstract}.
      optional {?m cdm:manifestation_manifests_expression ?expr; cdm:manifestation_type ?mtype.}
      optional {?m2  cdm:manifestation_manifests_expression ?expr; cdm:manifestation_has_thumbnail ?thumbnail }.
      ?expr cdm:expression_uses_language/dc:identifier ?lang1.
      optional { ?expr cdm:expression_title ?title_ }.
      {
          SELECT ?work ?date
          WHERE
          {
            {
              SELECT DISTINCT ?work ?date
              WHERE
                {
                
                ?work cdm:work_is_about_concept_eurovoc/skos:inScheme/dc:identifier "4026";

                    cdm:work_date_document ?date.                   
                 ?work cdm:work_created_by_agent/dc:identifier ?authorCode2 .
FILTER ( ?authorCode2 in ("CURIA","GCEU")).

                 } 
              
            }
          }
          ORDER BY DESC(?date)
          OFFSET 0
          LIMIT 10 }
      }
GROUP BY ?work ?expr ?date ?abstract ?lang1
ORDER BY DESC(?date) ?work
